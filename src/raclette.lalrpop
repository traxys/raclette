use crate::{ast::{Expr, Statement, Token, Literal, BinaryOp, Place, Folder}, span::SpannedValue};
use std::rc::Rc;
use std::collections::HashMap;

grammar;

extern {
	type Location = usize;
	type Error = crate::ast::UnknownToken;

	enum Token {
		number => Token::Number(<i64>),
		ident => Token::Identifier(<String>),
		str => Token::String(<String>),
		"->" => Token::Arrow,
		"\\" => Token::Backslash,
		"&" => Token::And,
		"|" => Token::Pipe,
		"|>" => Token::Redirect,
		"%" => Token::Percent,
		"@" => Token::At,
		"\n" => Token::Newline,
		"(" => Token::LParen,
		")" => Token::RParen,
		"[" => Token::LBracket,
		"]" => Token::RBracket,
		"{" => Token::LBrace,
		"}" => Token::RBrace,
		"." => Token::Dot,
		"=" => Token::Equal,
		"," => Token::Comma,
		"~" => Token::Tilde,
	}
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Spn<T>: SpannedValue<T> = {
	<start:@L> <value:T> <end:@R> => SpannedValue {start, value, end}
};

Literal: Literal = {
	number => Literal::Number(<>),
	str => Literal::String(<>),
};

Expr: Expr = {
	<lhs:Spn<Expr>> "|>" <rhs:Spn<FuncDef>> => {
		Expr::Binary(BinaryOp::Redirect, Box::new(lhs), Box::new(rhs))
	},
	<lhs:Spn<Expr>> "|>" <rhs:Spn<BOrExpr>> => {
		Expr::Binary(BinaryOp::Redirect, Box::new(lhs), Box::new(rhs))
	},
	<FuncDef> => <>,
	<BOrExpr> => <>,
};

FuncDef: Expr = {
	"\\" <args:Comma<ident>> "=" <ret:Spn<BOrExpr>> => Expr::FuncDef {
		args,
		ret: Box::new(ret),
	},
};

BOrExpr: Expr = {
	<lhs:Spn<BAndExpr>> "|" <rhs:Spn<BOrExpr>> => {
		Expr::Binary(BinaryOp::BitwiseOr, Box::new(lhs), Box::new(rhs))
	},
	<BAndExpr> => <>,
};

BAndExpr: Expr = {
	<lhs:Spn<MapExpr>> "&" <rhs:Spn<BAndExpr>> => {
		Expr::Binary(BinaryOp::BitwiseAnd, Box::new(lhs), Box::new(rhs))
	},
	<MapExpr> => <>,
};

MapField: (SpannedValue<Expr>, SpannedValue<Expr>) = {
	<Spn<Expr>> "=" <Spn<Expr>> => (<>),
};

StringMap: (SpannedValue<Expr>, SpannedValue<Expr>) = {
	<k:Spn<ident>> "=" <v:Spn<Expr>> => (k.map(Literal::String).wrap(Expr::Literal), v)
};

MapExpr: Expr = {
	"~" "{" <Comma<StringMap>> "}" => Expr::Map(<>),
	"{" <Comma<MapField>> "}" => Expr::Map(<>),
	<TildeExpr> => <>,
};

TildeExpr: Expr = {
	"~" <Spn<TildeExpr>> => Expr::Tilde(Box::new(<>)),
	<CallExpr> => <>,
};

Named: (String, SpannedValue<Expr>) = <ident> "=" <Spn<Expr>> => (<>);

NamedArgs: HashMap<String, SpannedValue<Expr>> = <args:(<Named> ",")*> <last:Named?> => {
	let mut named: HashMap<_, _> = args.into_iter().collect();
	if let Some(last) = last {
		named.insert(last.0, last.1);
	}
	named
};

PosArgs: Vec<SpannedValue<Expr>> = Comma<Spn<Expr>> => <>;

CallExpr: Expr = {
	<func:Spn<CallExpr>> "(" <args:PosArgs> ")" => Expr::Call {
		func: Box::new(func),
		args,
	},
	<NamedCallExpr> => <>,
};

NamedCallExpr: Expr = {
	<func:Spn<NamedCallExpr>> "{" <named:NamedArgs> "}" => Expr::NamedCall {
		func: Box::new(func),
		named,
	},
	<PrefixMapperExpr> => <>,
};

BinaryOp: BinaryOp = {
	"|" => BinaryOp::BitwiseOr,
	"&" => BinaryOp::BitwiseAnd,
	"|>" => BinaryOp::Redirect,
};

PrefixMapperExpr: Expr = {
	"@" <Spn<LiteralExpr>> => Expr::Mapper(Box::new(<>)),
	<LiteralExpr> => <>,
};

LiteralExpr: Expr = {
	<start:@L> "%" <op:BinaryOp> <end:@R> => Expr::Fold(SpannedValue {
		start,
		end,
		value: Folder::Operator(op)
	}),
	<start:@L> "%" "(" <func:Spn<Expr>> "," <def:Spn<Expr>> <_comma:(",")?> ")" <end:@R> => Expr::Fold(
		SpannedValue {
			start,
			end,
			value: Folder::Args{func: Box::new(func), def: Box::new(def)}
		}
	),
	<Spn<FuncDef>> "@" => Expr::Mapper(Box::new(<>)),
	<Spn<Literal>> => Expr::Literal(<>),
	<p:Spn<Place>> => Expr::Place(<>),
	"[" <Comma<Spn<Expr>>> "]" => Expr::Array(<>),
	"(" <Expr> ")" => <>,
};

Place: Place = {
	Spn<ident> => Place::Ident(<>),
	<e:Spn<LiteralExpr>> "." <f:Spn<ident>> => Place::Deref(Box::new(e), f.map(Rc::new)),
};

pub Statement: Statement = {
	<Spn<Expr>> => Statement::Expr(<>),
	<p:Spn<Place>> "=" <e:Spn<Expr>> => Statement::Assign(p, e),
};

pub File: Vec<Statement> = <mut r:(<Statement> "\n")*> <l:Statement?> => {
	match l {
		None => r,
		Some(v) => { r.push(v); r },
	}
};
