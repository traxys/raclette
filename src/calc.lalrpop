use crate::{
	ast::{
		Token,
		Expr,
		Literal,
		Variable,
		BinOp,
		BinOpKind,
		UnaryOp,
		UnaryOpKind,
		InputStatement,
		Function,
		Call,
		UserParseError,
	},
	span::SpannedValue,
};
use std::sync::Arc;

use lalrpop_util::ParseError;

grammar(source: &crate::span::MaybeNamed);

extern {
	type Location = usize;
	type Error = crate::ast::UserParseError;

	enum Token {
		"(" => Token::LParen,
		")" => Token::RParen,
		"|>" => Token::Redirect,
		"|" => Token::LOr,
		"&" => Token::LAnd,
		"^" => Token::LXor,
		"=" => Token::Equal,
		"+" => Token::Plus,
		"-" => Token::Minus,
		"*" => Token::Times,
		"**" => Token::Power,
		"/" => Token::Divide,
		"//" => Token::IntDivide,
		">>" => Token::RShift,
		"<<" => Token::LShift,
		"%" => Token::Percent,
		"\\" => Token::Backslash,
		"." => Token::Dot,
		":" => Token::Colon,
		"_(" => Token::UnitParen,
		"," => Token::Comma,
		"as" => Token::As,
		num => Token::Number(<rug::Integer>),
		float => Token::Float(<rug::Float>),
		ident => Token::Ident(<Arc<str>>),
		unit => Token::Unit(<Arc<str>>),
		binding => Token::Binding(<Arc<str>>),
	}
}

Spn<T>: SpannedValue<T> = {
    <start:@L> <value:T> <end:@R> => SpannedValue {
        start, 
        value, 
        end, 
        source: source.clone()
    }
};

Variable: Variable = ident+ => Variable(<>);
SpnBox<T>: Box<SpannedValue<T>> = Spn<T> => Box::new(<>);
Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

pub InputStatement: SpannedValue<InputStatement> = Spn<InputStatementInner> => <>;

InputStatementInner: InputStatement = {
	"|>" <Function> => InputStatement::LastRedirect(<>),
	Expr => InputStatement::Expr(<>),
	<SpnBox<Expr>> "as" <ManyUnit> => InputStatement::Expr(Expr::Dimensioned(<>)),
	"." <Spn<ident>> <CommandValue?> => InputStatement::Command(<>),
};

CommandValue: SpannedValue<Expr> = "=" <Spn<Expr>> => <>;

Function: Function = {
	Spn<Variable> => Function::Ref(<>),
};

pub Expr: Expr = {
	<Spn<Variable>> "=" <SpnBox<Expr>> => Expr::Assign(<>),
	ExprPipe => <>,
};

ExprPipe: Expr = {
	<val:Spn<ExprPipe>> "|>" <fun:Spn<Function>> => Expr::Call(Call {fun, args: vec![val]}),
	ExprLOr => <>,
};

ExprLOr: Expr = {
	<lhs:SpnBox<ExprShift>> "|" <rhs:SpnBox<ExprAdd>> => Expr::BinOp(BinOp{<>, kind: BinOpKind::LogicalOr}),
	ExprLXor => <>,
};

ExprLXor: Expr = {
	<lhs:SpnBox<ExprShift>> "^" <rhs:SpnBox<ExprAdd>> => Expr::BinOp(BinOp{<>, kind: BinOpKind::LogicalXor}),
	ExprLAnd => <>,
};

ExprLAnd: Expr = {
	<lhs:SpnBox<ExprShift>> "&" <rhs:SpnBox<ExprAdd>> => Expr::BinOp(BinOp{<>, kind: BinOpKind::LogicalAnd}),
	ExprShift => <>,
};

ExprShift: Expr = {
	<lhs:SpnBox<ExprShift>> "<<" <rhs:SpnBox<ExprAdd>> => Expr::BinOp(BinOp{<>, kind: BinOpKind::LeftShift}),
	<lhs:SpnBox<ExprShift>> ">>" <rhs:SpnBox<ExprAdd>> => Expr::BinOp(BinOp{<>, kind: BinOpKind::RightShift}),
	ExprAdd => <>,
};

ExprAdd: Expr = {
	<lhs:SpnBox<ExprAdd>> "+" <rhs:SpnBox<ExprMult>> => Expr::BinOp(BinOp{<>, kind: BinOpKind::Sum}),
	<lhs:SpnBox<ExprAdd>> "-" <rhs:SpnBox<ExprMult>> => Expr::BinOp(BinOp{<>, kind: BinOpKind::Diff}),
	ExprMult => <>,
};

ExprMult: Expr = {
	<lhs:SpnBox<ExprMult>> "/" <rhs:SpnBox<ExprUnary>> => Expr::BinOp(BinOp{<>, kind: BinOpKind::Divide}),
	<lhs:SpnBox<ExprMult>> "*" <rhs:SpnBox<ExprUnary>> => Expr::BinOp(BinOp{<>, kind: BinOpKind::Times}),
	ExprUnary => <>,
};

UnaryKind: UnaryOpKind = {
	"-" => UnaryOpKind::Minus,
	"+" => UnaryOpKind::Plus,
};

ExprUnary: Expr = {
	<kind:UnaryKind> <operand:SpnBox<ExprLiteral>> => Expr::UnaryOp(UnaryOp{<>}),
	ExprLiteral => <>,
};

I16: i16 = <n:Spn<num>> =>? match n.to_i16() {
	None => Err(ParseError::User {
		error: UserParseError::OutOfRange {
			ty: "i16",
			num: n.value,
			span: n.start..n.end,
		}
	}),
	Some(n) => Ok(n),
};

Unit: (Arc<str>, i16) = {
	unit => (<>, 1),
	<u:unit> <n:I16> => (<>),
	"(" <u:unit> "-" <e:I16> ")"=> (u, -e),
};

UnitId: (Arc<str>, i16) = {
	ident => (<>, 1),
	ident I16 => (<>),
	<u:ident> "-" <e:I16> => (u, (-e)),
	"/" <ident> => (<>, -1),
	"/" <u:ident> <e:I16> => (u, (-e)),
};

ManyUnit: Vec<SpannedValue<(Arc<str>, i16)>> = {
	Spn<Unit>+ => <>,
	"_(" <Spn<UnitId>+> ")" => <>,
};

Call: Call = {
	<fun:Spn<Function>> "(" <args:Comma<Spn<Expr>>> ")" => Call { <> },
};

LiteralExpr: Expr = {
	Spn<Literal> => Expr::Literal(<>),
};

ExprLiteral: Expr = {
	LiteralExpr => <>,
	"(" <SpnBox<Expr>> "as" <ManyUnit> ")" => Expr::Dimensioned(<>),
	Spn<Variable> => Expr::Variable(<>),
	Call => Expr::Call(<>),
	"(" <Expr> ")" => <>,
};

Literal: Literal = {
	num => Literal::Number(<>),
	float => Literal::Float(<>),
	":" <ident> => Literal::Atom(<>),
};
